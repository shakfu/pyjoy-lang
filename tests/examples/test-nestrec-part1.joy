2 setecho.
0 __settracegc.

(* Part 1: r-fact *)
DEFINE
    r-fact ==
        [ null ]
        [ pop 1]
        [ dup pred r-fact *]
        ifte.

[ 0 1 2 3 4 5 6 ]  [r-fact]  map.

(* Part 2: r-mcc91 *)
DEFINE
    r-mcc91 ==
        [ 100 > ]
        [ 10 - ]
        [ 11 + r-mcc91 r-mcc91 ]
        ifte.

[ -7 42 99 100 101 102 345 ]  [r-mcc91]  map.

(* Part 3: r-ack *)
DEFINE
    r-ack ==
        [ [ [pop null]  popd succ ]
          [ [null]  pop pred 1 r-ack ]
          [ [dup pred swap] dip pred r-ack r-ack ] ]
        cond.

[ [0 0] [0 1] [0 2] [0 3] [0 4] [0 5] ]   [i r-ack]  map  putln
[ [1 0] [1 1] [1 2] [1 3] [1 4] [1 5] ]   [i r-ack]  map  putln
[ [2 0] [2 1] [2 2] [2 3] [2 4] [2 5] ]   [i r-ack]  map  putln
[ [3 0] [3 1] [3 2] [3 3] [3 4] [3 5] ]   [i r-ack]  map  putln.
